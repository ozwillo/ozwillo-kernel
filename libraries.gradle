subprojects {
  repositories {
    mavenCentral()
  }
  configurations.all {
    resolutionStrategy {
      // AGPL-compatible alternatives (mostly Apache v2)
      def replacements = [
        "javax.annotation:jsr250-api": "org.apache.geronimo.specs:geronimo-annotation_1.2_spec:1.0",
        "org.jboss.spec.javax.annotation:jboss-annotations-api_1.2_spec": "org.apache.geronimo.specs:geronimo-annotation_1.2_spec:1.0",
        "org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec": "org.apache.geronimo.specs:geronimo-jaxrs_2.1_spec:1.1",
        "org.jboss.spec.javax.xml.bind:jboss-jaxb-api_2.3_spec": "org.apache.geronimo.specs:geronimo-jaxb_2.2_spec:1.0.1",
        "javax.activation:activation": "org.apache.geronimo.specs:geronimo-activation_1.1_spec:1.1",
        "javax.json.bind:javax.json.bind-api": "org.apache.geronimo.specs:geronimo-jsonb_1.0_spec:1.1",
        // Use an Apache-licensed clean-room reimplementation of JCIP Annotations.
        // The original JCIP Annotations are under Creative Commons Attribution License
        // which is not recommended for software, and incompatible with AGPL.
        // https://wiki.creativecommons.org/Frequently_Asked_Questions#Can_I_apply_a_Creative_Commons_license_to_software.3F
        "net.jcip:jcip-annotations": "com.github.stephenc.jcip:jcip-annotations:1.0-1",
      ]
      eachDependency { DependencyResolveDetails details ->
        def key = details.requested.group + ":" + details.requested.name
        def replacement = replacements[key]
        if (replacement) {
          details.useTarget(replacement)
        }
      }
    }
  }
}

def guiceVersion = "4.2.1"
def jacksonVersion = "2.9.7"
def log4jVersion = "2.11.1"
def resteasyVersion = "3.6.1.Final"

ext.libs = [
  guava:               "com.google.guava:guava:26.0-jre",
  jsr305:              "com.google.code.findbugs:jsr305:3.0.2",

  immutables:          "org.immutables:value:2.7.1",
  immutables_annotations: "org.immutables:value-annotations:2.7.1",

  forbiddenapis:       "de.thetaphi:forbiddenapis:2.6",

  bouncycastle:        "org.bouncycastle:bcprov-jdk15on:1.60",

  jose4j:              "org.bitbucket.b_c:jose4j:0.6.4",

  guice:               [
      "com.google.inject:guice:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jongo:               [
      "org.jongo:jongo:1.4.0",
      "de.undercouch:bson4jackson:2.9.2",
      "org.mongodb:mongo-java-driver:3.8.2",
    ],

  elasticsearch:       [
      "org.elasticsearch:elasticsearch:1.4.4",
      "io.searchbox:jest:1.0.3"
    ],

  resteasy_netty:      [
      "org.jboss.resteasy:resteasy-netty4:$resteasyVersion",
      "io.netty:netty-all:4.1.29.Final",
    ],

  resteasy_client:     [
      "org.jboss.resteasy:resteasy-client:$resteasyVersion",
      "net.ltgt.jaxrs:resteasy-client-okhttp3:1.1",
      "com.squareup.okhttp3:okhttp:3.11.0",
      "com.squareup.okhttp3:logging-interceptor:3.11.0"
    ],

  resteasy_testing:   "net.ltgt.jaxrs:resteasy-testing:0.2",

  jaxrs_webhook:      "net.ltgt.jaxrs:jaxrs-webhook:0.1",

  closure_templates_standalone: dependencies.create("com.google.template:soy:2018-03-14") {
      exclude group: "com.google.gwt"
      exclude group: "com.google.code.gson"
      exclude group: "org.json"
  },
  closure_templates:   [
      dependencies.create("com.google.template:soy:2018-03-14") {
          exclude group: "com.google.gwt"
          exclude group: "com.google.code.gson"
          exclude group: "org.json"
      },
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jackson_annotations: "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
  jackson_databind:    "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
  jackson:             [
      "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
      "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
      "com.fasterxml.jackson.datatype:jackson-datatype-guava:$jacksonVersion",
      "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion",
      "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion",
      "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jacksonVersion",
    ],

  jsSHA:               "org.webjars.bower:jsSHA:2.3.1",

  icu4j:               "com.ibm.icu:icu4j:62.1",

  galimatias:          "io.mola.galimatias:galimatias:0.2.1",

  mail:                "org.apache.geronimo.javamail:geronimo-javamail_1.6_mail:1.0.1",

  slf4j:               "org.slf4j:slf4j-api:1.7.25",

  log4j2:              "org.apache.logging.log4j:log4j-api:$log4jVersion",

  log4j_runtime:       [
      "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
      "com.lmax:disruptor:3.4.2",
    ],

  args4j:              "args4j:args4j:2.33",

  config:              "com.typesafe:config:1.3.3",

  junit:               "junit:junit:4.12",

  mockito:             "org.mockito:mockito-core:2.22.0",

  jukito:              [
      "org.jukito:jukito:1.5",
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
    ],

  assertj:             [
      "org.assertj:assertj-core:3.11.1",
      "org.assertj:assertj-guava:3.2.0",
    ],
]
